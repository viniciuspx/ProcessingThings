/* =============================================================================================================== */
/* =============================================================================================================== */

int maxy = 80;
int miny = -80;
int maxx = 80;
int minx = -80;
int maxz = 80;
int minz = -80;

int h = maxy - miny;
int w = maxx - minx;
int d = maxz - minz;

int lines = 8;
int columns = 4;
int llines = 12;
/* =============================================================================================================== */
/* =============================================================================================================== */

int[][] cube = {

  {-30, -30, -30, 1}, 
  {30, -30, -30, 1}, 
  {-30, 30, -30, 1}, 
  {30, 30, -30, 1}, 

  {-30, -30, 30, 1}, 
  {30, -30, 30, 1}, 
  {-30, 30, 30, 1}, 
  {30, 30, 30, 1}

};

int[][] L = {

  {0, 1}, 
  {0, 2}, 
  {2, 3}, 
  {1, 3}, 

  {4, 5}, 
  {4, 6}, 
  {6, 7}, 
  {7, 5}, 

  {2, 6}, 
  {0, 4}, 
  {1, 5}, 
  {3, 7}

};

/* =============================================================================================================== */
/* =============================================================================================================== */



void linDDA(int xi, int yi, int xf, int yf) {

  int dx = xf - xi, dy = yf - yi, steps, k;

  float incX, incY, x = xi, y = yi;

  if (abs(dx) > abs(dy)) steps = abs(dx);
  else steps = abs(dy);

  incX = dx / (float) steps;
  incY = dy / (float) steps;

  point((int)x, (int)y);

  for (k = 0; k < steps; k++) {

    x += incX;
    y += incY;  

    point((int)x, (int)y);
  }
}

int[][] multiplyMatrix(int[][] M, float[][]N, int m, int n, int s) {

  int R[][] = new int[m][s];

  for (int i=0; i<m; ++i) for (int j=0; j<n; ++j) for (int k=0; k<s; ++k) {
    R[i][j] += int(M[i][k] * N[k][j]);
  } 

  return R;
}

void debPrintM (int[][] M, int l, int c) {

  print(" ------------Matrix------------ \n");

  for (int i = 0; i < l; i ++) {
    for (int j = 0; j < c; j ++) {
      print(M[i][j] + " ");
    }
    print("\n");
  }
}

void renderP(int[][] M, int[][] L, int l) {
  for (int i = 0; i < l; i ++) {
    stroke(0);
    linDDA(M[L[i][0]][0], M[L[i][0]][1], M[L[i][1]][0], M[L[i][1]][1]);
  }
}

int[][] getMatrixT(int[][] L, int n, int m) {

  int R[][] = new int[n][m];

  for (int i = 0; i < n; i ++) R[i] = transformCoords(L[i][0], L[i][1], L[i][2]);

  return R;
}


/* =============================================================================================================== */
/* =============================================================================================================== */

int[] transformCoords(int x, int y, int z) {

  int[] coords = {0, 0, 0, 1};

  float x_l, y_l, z_l;
  float x_ll, y_ll, z_ll;

  x_l = x - minx;
  y_l = y - miny;
  z_l = z - minz;

  float m = min(width/w, height/h);

  x_ll = x_l;
  y_ll = h - y_l;
  z_ll = d - z_l;

  coords[0] = int(x_ll*m + (width - w*m)/2);
  coords[1] = int(y_ll*m + (height - w*m)/2);
  coords[2] = int(z_ll*m);

  return coords;
}

int[][] cavaleiraProj(int M[][], int n, int m) {

  float[][] C = {{1, 0, 0, 0}, {0, 1, 0, 0}, {sqrt(2)/2*0.5, -sqrt(2)/2*0.5, 1, 0}, {0, 0, 0, 1}};

  M = multiplyMatrix(M, C, n, m, 4);

  return M;
}

int[][] cabinetProj(int M[][], int n, int m) {

  float[][] C = {{1, 0, 0, 0}, {0, 1, 0, 0}, {sqrt(2)/4*0.5, -sqrt(2)/4*0.5, 1, 0}, {0, 0, 0, 1}};

  M = multiplyMatrix(M, C, n, m, 4);

  return M;
}

int[][] isometricProj(int M[][], int n, int m) {

  float[][] I = {{sqrt(2)/2, 1/sqrt(6), 0, 0}, {0, 2/sqrt(6), 0, 0}, {sqrt(2)/2, -1/sqrt(6), 0, 0}, {0, 0, 0, 1}};

  M = multiplyMatrix(M, I, n, m, 4);

  return M;
}

int[][] escapepointProj(int M[][], int n, int m, int fx, int fy, int fz) {
  
  float a = 0;
  float b = 0;
  float c = 0;
  
  if(fx != 0) a = -1/fx;
  if(fy != 0) b = -1/fy;
  if(fz != 0) c = -1/fz;
  
  float[][] EC = {{1, 0, 0, a}, {0, 1, 0, b}, {0, 0, 1, c}, {0, 0, 0, 1}};

  M = multiplyMatrix(M, EC, n, m, 4);

  return M;
}

int[][] homogenize(int M[][], int n, int m) {

  for (int i = 0; i < n; i++) {
    for (int j = 0; j < m; j++) {
      M[i][j] /= M[i][m-1];
    }
  }

  return M;
}

int[][] rotateM(int M[][], int n, int m, float rz, float rx, float ry) {

  float[][] R1 = {{cos(rz), sin(rz), 0, 0}, {-sin(rz), cos(rz), 0, 0}, {0, 0, 1, 0}, {0, 0, 0, 1}};
  float[][] R2 = {{1, 0, 0, 0}, {0, cos(rx), sin(rx), 0}, {0, -sin(rx), cos(rx), 0}, {0, 0, 0, 1}};
  float[][] R3 = {{cos(ry), 0, -sin(ry), 0}, {0, 1, 0, 0}, {sin(ry), 0, cos(ry), 0}, {0, 0, 0, 1}};

  M = multiplyMatrix(M, R1, n, m, 4);
  M = multiplyMatrix(M, R2, n, m, 4);
  M = multiplyMatrix(M, R3, n, m, 4);

  return M;
}

int[][] translateM(int M[][], int n, int m, float tz, float tx, float ty) {

  float[][] T = {{1, 0, 0, 0}, {0, 1, 0, 0}, {0, 0, 1, 0}, {tx, ty, tz, 1}};

  M = multiplyMatrix(M, T, n, m, 4);

  return M;
}

int[][] scaleM(int M[][], int n, int m, float sz, float sx, float sy) {

  float[][] S = {{sx, 0, 0, 0}, {0, sy, 0, 0}, {0, 0, sz, 0}, {1, 1, 1, 1}};

  M = multiplyMatrix(M, S, n, m, 4);

  return M;
}

/* =============================================================================================================== */
/* =============================================================================================================== */

float rz = 0;
float rx = 0;
float ry = 0;

float tz = 0;
float tx = 0;
float ty = 0;

float sz = 1;
float sx = 1;
float sy = 1;

float fx = 5;
float fy = 0;
float fz = 5;

PFont f;

int p = 0;

String text = "Comandos \n WASDQE - Transladar \n TFGHRY - Rotacionar \n 1 a 8 - Escalonar \n O - Origem";

void draw() {

  background(255);

  int M[][] = new int[lines][columns];

  M = rotateM(cube, lines, columns, rz, rx, ry);

  M = scaleM(M, lines, columns, sz, sx, sy);

  M = translateM(M, lines, columns, tz, tx, ty);

  M = getMatrixT(M, lines, columns);


  switch(p) {
  case 0:
    M = cavaleiraProj(M, lines, columns);   
    break;
  case 1:
    M = cabinetProj(M, lines, columns);   
    break;
  case 2:
    M = isometricProj(M, lines, columns);   
    break;
  case 3:
    M = escapepointProj(M, lines, columns, 0, 0, fz);
      debPrintM(M,lines,columns);
    //M = homogenize(M, lines, columns);
    break;
  case 4:
    M = escapepointProj(M, lines, columns, fx, 0, fz);
    //M = homogenize(M, lines, columns);
    break;
  }

  renderP(M, L, llines);

  textAlign(TOP);
  textFont(f);
  text(text, 5, 45); 
  fill(200, 0, 0);
}

void setup() {
  size(1200, 800);
  background(255);
  f = createFont("Arial", 18, true);
}

void keyPressed() {
  if (key == 'q' || key == 'Q') {
    tz += 1;
  }
  if (key == 'e' || key == 'E') {
    tz -= 1;
  }
  if (key == 'd' || key == 'D') {
    tx += 1;
  }
  if (key == 'a' || key == 'A') {
    tx -= 1;
  }
  if (key == 'w' || key == 'W') {
    ty += 1;
  }
  if (key == 's' || key == 'S') {
    ty -= 1;
  }

  if (key == 'f' || key == 'F') {
    rz += 0.1;
  }
  if (key == 'h' || key == 'H') {
    rz -= 0.1;
  }
  if (key == 'g' || key == 'G') {
    rx += 0.1;
  }
  if (key == 't' || key == 'T') {
    rx -= 0.1;
  }
  if (key == 'y' || key == 'Y') {
    ry += 0.1;
  }
  if (key == 'r' || key == 'R') {
    ry -= 0.1;
  }

  if (key == '1') {
    sx += 0.1;
  }
  if (key == '3') {
    sy += 0.1;
  }
  if (key == '5') {
    sz += 0.1;
  }
  if (key == '7') {
    sx += 0.1;
    sy += 0.1;
    sz += 0.1;
  }

  if (key == '2') {
    sx -= 0.1;
  }
  if (key == '4') {
    sy -= 0.1;
  }
  if (key == '6') {
    sz -= 0.1;
  }
  if (key == '8') {
    sx -= 0.1;
    sy -= 0.1;
    sz -= 0.1;
  }

  if (key == 'o' || key == 'O' ) {
    rz = 0;
    rx = 0;
    ry = 0;

    tz = 0;
    tx = 0;
    ty = 0;

    sz = 1;
    sx = 1;
    sy = 1;
  }

  if (key == 'p') {
    p += 1;
    if(p == 5) p = 0;
  }
}
